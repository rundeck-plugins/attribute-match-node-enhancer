plugins{
	//defines scmVersion from git tags
    id 'java'
    id 'groovy'
    id 'idea'
    alias(libs.plugins.axionRelease)
}
apply plugin: 'idea'
ext.pluginClassNames = 'org.rundeck.plugins.nodes.icon.IconNodeEnhancer,org.rundeck.plugins.nodes.attributes.AttributeNodeEnhancer'
ext.pluginName = 'Icon Nodes Enhancer'
ext.pluginDescription = 'Adds icons to nodes based on attribute values'
scmVersion {
    tag {
    	//defines release tags like "v1.2.3"
        prefix = 'v'
        versionSeparator = ''
    }
    ignoreUncommittedChanges = true
}

allprojects {
    project.version = scmVersion.version
    apply from: "${rootDir}/gradle/java.gradle"
}


jar {
    manifest {
        attributes 'Rundeck-Plugin-Version': '2.0',
                   'Rundeck-Plugin-Archive': 'true',
                   'Rundeck-Plugin-File-Version': project.version,
                   'Rundeck-Plugin-Author': 'Greg Schueler',
                   'Rundeck-Plugin-URL': 'http://rundeck.com',
                   'Rundeck-Plugin-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssX"),
                   'Rundeck-Plugin-Classnames': pluginClassNames,
                   'Rundeck-Plugin-Name': pluginName,
                   'Rundeck-Plugin-Description': pluginDescription,
                   'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.14+'


    }
}
dependencies {
    implementation(libs.rundeckCore) {
        exclude group: "com.google.guava"
    }

    testImplementation libs.bundles.testLibs
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

