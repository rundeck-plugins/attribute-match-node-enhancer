plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    alias(libs.plugins.axionRelease)
    alias(libs.plugins.nexusPublish)
}

group = 'org.rundeck.plugins'
ext.pluginClassNames = 'org.rundeck.plugins.nodes.icon.IconNodeEnhancer,org.rundeck.plugins.nodes.attributes.AttributeNodeEnhancer'
ext.pluginName = 'Attribute Node Enhancer'
ext.publishDescription = 'Adds icons to nodes based on attribute values'
ext.publishName = "Attribute Node Enhancer ${project.version}"
ext.githubSlug = 'rundeck-plugins/attribute-match-node-enhancer'
ext.developers = [
        [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]

scmVersion {
    tag {
    	//defines release tags like "v1.2.3"
        prefix = 'v'
        versionSeparator = ''
    }
    ignoreUncommittedChanges = true
}

allprojects {
    project.version = scmVersion.version
    apply from: "${rootDir}/gradle/java.gradle"
}


jar {
    manifest {
        attributes 'Rundeck-Plugin-Version': '2.0',
                   'Rundeck-Plugin-Archive': 'true',
                   'Rundeck-Plugin-File-Version': project.version,
                   'Rundeck-Plugin-License': 'Apache 2.0',
                   'Rundeck-Plugin-Author': 'Greg Schueler',
                   'Rundeck-Plugin-URL': 'http://rundeck.com',
                   'Rundeck-Plugin-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssX"),
                   'Rundeck-Plugin-Classnames': pluginClassNames,
                   'Rundeck-Plugin-Name': pluginName,
                   'Rundeck-Plugin-Description': publishDescription,
                   'Rundeck-Plugin-Rundeck-Compatibility-Version': '3.0.14+'


    }
}
test {
    useJUnitPlatform()
}

dependencies {
    implementation(libs.rundeckCore) {
        exclude group: "com.google.guava"
    }
    
    // Add secure commons-lang3 to provide alternative to vulnerable commons-lang 2.6
    implementation(libs.commonsLang3)

    testImplementation libs.bundles.testLibs
}

configurations.all {
    resolutionStrategy {
        // Replace vulnerable commons-lang with secure commons-lang3
        dependencySubstitution {
            substitute module('commons-lang:commons-lang') using module("org.apache.commons:commons-lang3:${libs.versions.commonsLang3.get()}")
        }
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

nexusPublishing {
    packageGroup = 'org.rundeck.plugins'
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}


apply from: "${rootDir}/gradle/publishing.gradle"
